apply plugin: 'java'
apply plugin: 'idea'

sourceCompatibility = 1.7
targetCompatibility = 1.7

configurations {
    provided
}

dependencies {
    compile project(':requery')
    provided project(':requery-processor')
    compile fileTree(include: ['*.jar'], dir: 'libs') // for Oracle/SQL server JDBC drivers
    compile 'io.reactivex:rxjava:1.1.1'
    compile 'org.eclipse.persistence:javax.persistence:2.1.1'
    testCompile fileTree(include: ['*.jar'], dir: 'test-libs')
    testCompile 'org.openjdk.jmh:jmh-core:1.11.2'
    testCompile 'org.openjdk.jmh:jmh-generator-annprocess:1.11.2'
    testCompile 'mysql:mysql-connector-java:5.1.36'
    testCompile 'org.postgresql:postgresql:9.2-1004-jdbc41'
    testCompile 'org.xerial:sqlite-jdbc:3.8.11.1'
    testCompile 'org.apache.derby:derby:10.10.1.1'
    testCompile 'com.h2database:h2:1.4.188'
    testCompile 'org.hsqldb:hsqldb:2.3.3'
    testCompile 'org.ehcache:ehcache:3.0.0.m3'
    testCompile 'javax.cache:cache-api:1.0.0'
    compile 'junit:junit:4.12'
}

idea {
    module {
        scopes.PROVIDED.plus += [configurations.provided]
    }
}

sourceSets {
    main {
        runtimeClasspath += configurations.provided
    }
    generated {
        java {
            srcDirs += "$projectDir/src/main/generated"
        }
    }
}

// custom task to run apt (if you use compile or provided dependency scope
// gradle will compile and export all the processor dependencies which is wrong)
// https://issues.gradle.org/browse/GRADLE-784
task aptProcess(type: JavaCompile, group: 'build', description: 'apt') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.provided
    options.compilerArgs = [
            "-processor", "io.requery.processor.EntityProcessor"
    ]
    destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

compileJava {
    dependsOn aptProcess
    source aptProcess.destinationDir
}

clean {
    delete sourceSets.generated.java.srcDirs
}
